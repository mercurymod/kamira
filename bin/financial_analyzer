#!/usr/bin/env coffee

program = require 'commander'

program
  .option('-m, --measure <measureId>', 'specify a particular measure')
  .option('-l, --logging', 'turn on logging')
  .parse(process.argv)

_ = require 'underscore'

mongoose = require 'mongoose'
mongoose.connect 'mongodb://localhost/cypress_development'

Measure = require '../models/measure'
ValueSet = require '../models/value_set'
Cost = require '../models/cost'

measureCosts = {}

filter = {}
if program.measure
  filter["nqf_id"] = {"$in": program.measure.split(',') }

Cost.find (err, costs) ->

  # Map cost info to format currently expected by cost calculator
  oidCosts = {}
  for cost in costs
    oidCosts[cost.oid] =
      min: cost.firstQuartile
      max: cost.thirdQuartile

  ValueSet.find (err, valueSets) ->

    # Map oids to code sets to we can later display code set info on what's missing
    oidCodeSets = {}
    for valueSet in valueSets
      oidCodeSets[valueSet.oid] = _(c.code_system_name for c in valueSet.concepts).uniq()

    Measure.find(filter).sort('nqf_id').exec (err, measures) ->

      summary = []
    
      for measure in measures
        # PWKFIX Remove _doc when schema expansion is checked in
        [costs, details] = measure.calculateNumeratorCosts oidCosts, logging: program.logging

        header = "#{measure._doc.nqf_id}#{measure._doc.sub_id || ''}  #{measure.name}"
        console.log Array(header.length+1).join('=')
        console.log header
        console.log Array(header.length+1).join('=')
        console.log ''

        if costs
          console.log "Measure numerator cost: firstQuartile: $#{costs.min}  thirdQuartile: $#{costs.max}"
        else
          console.log "Unable to calculate costs for measure"

        withCostInfo = ("#{code} #{detail.title}:  firstQuartile: $#{detail.costs.min}  thirdQuartile: $#{detail.costs.max}" for code, detail of details when detail.costs)
        if withCostInfo.length > 0
          console.log "\nCost information is available for the following OIDS:\n"
          console.log "  #{oidDetail}" for oidDetail in withCostInfo

        withoutCostInfo = ("#{code} #{detail.title} [#{oidCodeSets[code]}]" for code, detail of details when !detail.costs)
        if withoutCostInfo.length > 0
          console.log "\nNo cost information is available for the following OIDS:\n"
          console.log "  #{oidDetail}" for oidDetail in withoutCostInfo

        console.log ''

        summary.push("#{header}: firstQuartile: $#{costs.min}  thirdQuartile: $#{costs.max}") if costs

      console.log "=========================="
      console.log "Summary of numerator costs"
      console.log "=========================="
      console.log line for line in summary

      mongoose.disconnect()
